plugins {
    id 'java'
    id 'org.springframework.boot' version '3.1.4'
    id 'io.spring.dependency-management' version '1.1.3'
    id 'checkstyle'
}

allprojects {
    repositories {
        mavenCentral()
    }
}

test {
    useJUnitPlatform()
}

checkstyle {
    toolVersion = "10.9"
    configFile = file("${rootDir}/config/checkstyle/checkstyle.xml")
}
bootJar.enabled = false

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    group = 'com.planting'
    version = '0.0.1-SNAPSHOT'
    java {
        sourceCompatibility = '17'
    }

    ext {
        set('springCloudVersion', "2022.0.4")
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    repositories {
        mavenCentral()
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }

    tasks.named('test') {
        useJUnitPlatform()
    }
}


['gateway-service', 'auth-service', 'eureka-service', 'plan-service'].each {
    project(it) {
        dependencies {
            implementation 'org.springframework.cloud:spring-cloud-starter-config'
        }

        sourceSets {
            main {
                resources {
                    srcDirs += rootProject.file('resources')
                }
            }
        }

        processResources {
            filesMatching("**/application.yml") {
                expand(project: project.name)
            }
        }
    }
}

['gateway-service', 'auth-service', 'config-service', 'eureka-service', 'plan-service'].each {
    project(it) {
        dependencies {
            implementation 'org.springframework.boot:spring-boot-starter-actuator'
            implementation 'org.springframework.cloud:spring-cloud-starter-bus-amqp'
            testImplementation 'org.springframework.boot:spring-boot-starter-test'
        }
    }
}

['gateway-service', 'auth-service', 'plan-service'].each {
    project(it) {
        dependencies {
            implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
        }
    }
}

bootJar {
    enabled = false
}

jar {
    enabled = true
}
